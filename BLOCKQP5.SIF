***************************
* SET UP THE INITIAL DATA *
***************************

NAME          BLOCKQP5 

*   Problem :
*   *********

*   A nearly degenerate, non-convex quadratic program with some structure.

*   The objective function is of the form
*
*      sum (i=1,n) (i/n) ( x_i y_i ) + sum(j=1,b) z_j^2
*
*   (cf BLOCKQP1.SIF where the factor (i/n) is missing).
*   There are n equality constraints of the form
*
*        x_i + y_i + sum (j=1,b) z_j = b   
*
*   There is an inequality constraint of the form
*
*      sum(i=1,n) x_i + y_i + sum(j=1,b) z_j >= b + 1
*
*   Finally, there are simple bounds
*
*         1 <= x_i, y_i <= 1    (i=1,n)
*         0 <= z_j <= 1         (j=1,b)
*
*   (cf BLOCKQP3.SIF where the constraint 0 <= z_j <= 2)

*   SIF input: Nick Gould, June 1994

*   classification QLR2-AN-V-V

*   The number of equality constraints

*IE N                   10             $-PARAMETER
*IE N                   100            $-PARAMETER
*IE N                   1000           $-PARAMETER     original value
 IE N                   5000           $-PARAMETER
*IE N                   10000          $-PARAMETER

*   The number of block-variables 

*IE B                   5              $-PARAMETER     original value
 IE B                   10             $-PARAMETER
*IE B                   50             $-PARAMETER

*  Other useful values.

 IE 1                   1
 RI RB        B
 RA RB+1      RB        1
 RI RN        N

VARIABLES

 DO I         1                        N
 X  X(I)
 X  Y(I)
 ND

 DO J         1                        B
 X  Z(J)
 ND

GROUPS

 N  OBJ

 DO I         1                        N
 XG I         X(I)      1.0            Y(I)      1.0           
 XE E(I)      X(I)      1.0            Y(I)      1.0           
 ND

 DO J         1                        B
 XG I         Z(J)      1.0
 DO I         1                        N
 XE E(I)      Z(J)      1.0
 ND

CONSTANTS

 DO I         1                        N
 ZE BLOCKQP5  E(I)                     RB
 ND

 ZE BLOCKQP5  I                        RB+1

BOUNDS

 DO I         1                        N
 XL BLOCKQP5  X(I)      -1.0
 XU BLOCKQP5  X(I)      1.0
 XL BLOCKQP5  Y(I)      -1.0
 XU BLOCKQP5  Y(I)      1.0
 ND

 DO J         1                        B
 XL BLOCKQP5  Z(J)      0.0
 XU BLOCKQP5  Z(J)      1.0
 ND

START POINT

 XV BLOCKQP5  'DEFAULT' 0.5

ELEMENT TYPE

 EV SQ        Z
 EV PROD      X                        Y

ELEMENT USES

 DO I         1                        N
 XT P(I)      PROD
 ZV P(I)      X                        X(I)
 ZV P(I)      Y                        Y(I)
 ND

 DO I         1                        B
 XT S(I)      SQ
 ZV S(I)      Z                        Z(I)
 ND

GROUP USES

 DO I         1                        N
 RI RI        I
 R/ I/N       RI                       RN
 ZE OBJ       P(I)                     I/N
 ND

 DO J         1                        B
 XE OBJ       S(J)
 ND

OBJECT BOUND

*   Solution

*LO SOLTN               -2.4987D-1     $ (n=10,b=5)
*LO SOLTN               -4.75D+1       $ (n=100,b=5)
*LO SOLTN               -4.975D+2      $ (n=1000,b=5)

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      BLOCKQP5

INDIVIDUALS

 T  SQ
 F                      0.5 * Z * Z
 G  Z                   Z
 H  Z         Z         1.0

 T  PROD
 F                      X * Y
 G  X                   Y
 G  Y                   X
 H  X         Y         1.0

ENDATA

