***************************
* SET UP THE INITIAL DATA *
***************************

NAME          PINENE  

*   Problem :
*   *********

*   Determine the reaction coefficients in the thermal isometrization 
*   of alpha-Pinene. The linear kinetic model is

*     y_1' = - (theta_1 + theta_2) y_1
*     y_2' = theta_1 y_1
*     y_3' = theta_2 y_1 - ( theta_3 + theta_4 ) y_3 + theta_5 y_5
*     y_4' = theta_3 y_3
*     y_5' = theta_4 y_3 - theta_5 y_5

*   where the reaction coefficients theta_i are positive,
*   with given initial conditions. The problem is to minimize

*    sum{i=1,8} || y(tau_i,theta) - z_i||^2

*   where the z_i are concentration measurements for y at times tau_i (i=1,8)

*   This is problem 5 in the COPS (Version 2) collection of 
*   E. Dolan and J. More'
*   see "Benchmarking Optimization Software with COPS"
*   Argonne National Labs Technical Report ANL/MCS-246 (2000)

*   SIF input: Nick Gould, November 2000

*   classification OOR2-AN-V-V

* The number of differential equations

 IE NE                  5

*   The number of subintervals

*IE NH                  25             $-PARAMETER
*IE NH                  50             $-PARAMETER
*IE NH                  100            $-PARAMETER
 IE NH                  200            $-PARAMETER

* The number of ODE parameters

 IE NP                  5              $-PARAMETER

* The number of measurements

 IE NM                  8              $-PARAMETER

*  The number of collocation points 

*IE NC                  1              $-PARAMETER
*IE NC                  2              $-PARAMETER
 IE NC                  3              $-PARAMETER
*IE NC                  4              $-PARAMETER

* roots of NC-th degree Legendre polynomial

*RE RHO1                0.5            $-PARAMETER NC = 1 
*RE RHO1                0.7886751346   $-PARAMETER NC = 2 
 RE RHO1                0.5            $-PARAMETER NC = 3 
*RE RHO1                0.0694318442   $-PARAMETER NC = 4

*RE RHO2                0.2113248654   $-PARAMETER NC = 2 
 RE RHO2                0.8872983346   $-PARAMETER NC = 3 
*RE RHO2                0.3300094782   $-PARAMETER NC = 4

 RE RHO3                0.1127016654   $-PARAMETER NC = 3 
*RE RHO3                0.6699905218   $-PARAMETER NC = 4

*RE RHO4                0.9305681558   $-PARAMETER NC = 4

* times at which observations made

 RE TAU1                1230.0
 RE TAU2                3060.0
 RE TAU3                4920.0
 RE TAU4                7800.0
 RE TAU5                10680.0
 RE TAU6                15030.0
 RE TAU7                22620.0
 RE TAU8                36420.0

*  The ODE is defined in [0,TF]

 A= TF        TAU(NM)

*   The uniform interval length

 RI RNH       NH
 R/ H         TF                       RNH

*  Concentrations

 RE Z1,1                88.35
 RE Z2,1                76.4 
 RE Z3,1                65.1 
 RE Z4,1                50.4 
 RE Z5,1                37.5 
 RE Z6,1                25.9 
 RE Z7,1                14.0 
 RE Z8,1                4.5 

 RE Z1,2                7.3
 RE Z2,2                15.6
 RE Z3,2                23.1
 RE Z4,2                32.9
 RE Z5,2                42.7
 RE Z6,2                49.1
 RE Z7,2                57.4
 RE Z8,2                63.1

 RE Z1,3                2.3
 RE Z2,3                4.5
 RE Z3,3                5.3
 RE Z4,3                6.0
 RE Z5,3                6.0
 RE Z6,3                5.9
 RE Z7,3                5.1
 RE Z8,3                3.8

 RE Z1,4                0.4
 RE Z2,4                0.7
 RE Z3,4                1.1
 RE Z4,4                1.5
 RE Z5,4                1.9
 RE Z6,4                2.2
 RE Z7,4                2.6
 RE Z8,4                2.9

 RE Z1,5                1.75
 RE Z2,5                2.8 
 RE Z3,5                5.8 
 RE Z4,5                9.3 
 RE Z5,5                12.0 
 RE Z6,5                17.0 
 RE Z7,5                21.0 
 RE Z8,5                25.7

*  ODE initial conditions

 RE BC1                 100.0   
 RE BC2                 0.0   
 RE BC3                 0.0   
 RE BC4                 0.0   
 RE BC5                 0.0   

*  Other useful values

 IE 1                   1
 IE 2                   2
 IE 3                   3
 IE 4                   4
 IE 5                   5
 IA NH-1      NH        -1                
 
*  Factorials

 RE FACT0               1.0
 DO I         1                        NC
 RI RI        I
 IA I-1       I         -1
 A* FACT(I)   FACT(I-1)                RI
 ND

* itau[i] is the largest integer k with t[k] <= tau[i]
* itau {i in 1..nm} := min(nh,floor(tau[i]/h)+1);    

 DO I         1                        NM
 A/ TAU/H     TAU(I)                   H
 IR IT/H      TAU/H
 IA IT/H+1    IT/H      1
 I= A         IT/H+1
 I= B         NH

*  this is how we find min(a,b) !!

*  first set a <- - a and b <- - b
 IM A         A         -1
 IM B         B         -1
*  now add |a| + |b| to both
 RI A         A  
 R( ABSA      ABS                      A
 IR ABSA      ABSA
 RI B         B
 R( ABSB      ABS                      B
 IR ABSB      ABSB
 I+ ABSA+B    ABSA                     ABSB
 I+ A         A                        ABSA+B
 I+ B         B                        ABSA+B
* only one of [a/b] and [b/a] is nonzero (unless a=b)
 I/ A/B       A                        B
 I/ B/A       B                        A
* form max(a,b) = (a.[a/b] + b.[b/a]) / ([a/b] + [b/a])
 I+ SUM       A/B                      B/A
 I* A         A                        A/B
 I* B         B                        B/A
 I+ MAXA,B    A                        B
 I/ MAXA,B    MAXA,B                   SUM
* subtract |a| + |b| and negate to find the required minimum
 I- MINA,B    ABSA+B                   MAXA,B

 AI ITAU(I)   MINA,B
 ND

VARIABLES

 DO I         1                        NP
 X  THETA(I)  $ ODE parameters
 ND

*  The collocation approximation u is defined by the parameters V and W

 DO I         1                        NH
 DO J         1                        NE
 X  V(I,J)
 OD J

 DO K         1                        NC
 DO S         1                        NE
 X  W(I,K,S)
 OD S
 OD K

 DO J         1                        NC

* U[i,j,s] is u evaluated  at the collocation points
* DU[i,j,s] is u' evaluated  at the collocation points

 X  U(I,J,1)
 X  DU(I,J,1)
 X  DU(I,J,2)
 X  U(I,J,3)
 X  DU(I,J,3)
 X  DU(I,J,4)
 X  U(I,J,5)
 X  DU(I,J,5)

 ND

GROUPS

* param t {i in 1..nh+1} := (i-1)*h
*  obj(j,s) =  v[itau[j],s] - z[j,s] + (
*     sum {k in 1..nc} w[itau[j],k,s]*(tau[j]-t[itau[j]])^k/(fact[k]*h^(k-1)))

 DO J         1                        NM
 A= RITAU     ITAU(J)
 IR I         RITAU
 RA T         RITAU     -1.0
 R* T         T                        H
 A- DIFF      TAU(J)                   T

 DO S         1                        NE
 R= RATIO     DIFF
 XN OBJ(J,S)  V(I,S)    1.0

 DO K         1                        NC
 A/ COEF      RATIO                    FACT(K)
 ZN OBJ(J,S)  W(I,K,S)                 COEF
 R* RATIO     RATIO                    DIFF
 R/ RATIO     RATIO                    H
 ND

* - u {i in 1..nh, j in 1..nc, s in 1..ne} + 
*  v[i,s] + h*sum {k in 1..nc} w[i,k,s]*(rho[j]^k/fact[k]) = 0

 DO I         1                        NH
 DO J         1                        NC
 A= RH        RHO(J)

 XE U(I,J,1)  U(I,J,1)  -1.0           V(I,1)    1.0      
 XE U(I,J,3)  U(I,J,3)  -1.0           V(I,3)    1.0      
 XE U(I,J,5)  U(I,J,5)  -1.0           V(I,5)    1.0      

 R* PROD      RH                       H
 DO K         1                        NC
 A/ COEF      PROD                     FACT(K)
 ZE U(I,J,1)  W(I,K,1)                 COEF
 ZE U(I,J,3)  W(I,K,3)                 COEF
 ZE U(I,J,5)  W(I,K,5)                 COEF
 R* PROD      PROD                     RH
 OD K

* - du {i in 1..nh, j in 1..nc, s in 1..ne} +
*   sum {k in 1..nc} w[i,k,s]*(rho[j]^(k-1)/fact[k-1]) = 0
 
 XE DU(I,J,1) DU(I,J,1) -1.0
 XE DU(I,J,2) DU(I,J,2) -1.0
 XE DU(I,J,3) DU(I,J,3) -1.0
 XE DU(I,J,4) DU(I,J,4) -1.0
 XE DU(I,J,5) DU(I,J,5) -1.0

 RE PROD                1.0
 DO K         1                        NC
 IA K-1       K         -1
 A/ COEF      PROD                     FACT(K-1)
 ZE DU(I,J,1) W(I,K,1)                 COEF
 ZE DU(I,J,2) W(I,K,2)                 COEF
 ZE DU(I,J,3) W(I,K,3)                 COEF
 ZE DU(I,J,4) W(I,K,4)                 COEF
 ZE DU(I,J,5) W(I,K,5)                 COEF
 R* PROD      PROD                     RH

 ND

*  continuity {i in 1..nh-1, s in 1..ne}:  
*   v[i,s] - v[i+1,s] + (sum {j in 1..nc} w[i,j,s]*h/fact[j]) = 0

 DO I         1                        NH-1
 IA I+1       I         1
 DO S         1                        NE
 XE C(I,S)    V(I,S)    1.0            V(I+1,S)  -1.0
 DO J         1                        NC
 A/ COEF      H                        FACT(J)
 ZE C(I,S)    W(I,J,S)                 COEF
 ND

* collocation eqns (linear parts)

 DO I         1                        NH
 DO J         1                        NC
 DO S         1                        NE
 XE CO(I,J,S) DU(I,J,S) 1.0
 ND

CONSTANTS

 DO J         1                        NM
 DO S         1                        NE
 Z  PINENE    OBJ(J,S)                 Z(J,S)
 ND

BOUNDS

 XR PINENE    'DEFAULT'

 DO I         1                        NP
 XL PINENE    THETA(I)  0.0
 ND

 DO S         1                        NE
 ZX PINENE    V(1,S)                   BC(S)
 ND

START POINT

 DO I         1                        NP
 X  PINENE    THETA(I)  0.0
 ND

 DO I         1                        NH
 DO J         1                        NE
 X  PINENE    V(I,J)
 OD J
 ND

 IE I1                  1
 A= RITAU     ITAU(1)
 IR I2        RITAU

 DO I         I1                       I2
 IE S                   1
 Z  PINENE    V(I,S)                   BC(S)
 DO J         1                        NC
 X  PINENE    W(I,J,S)  0.0
 Z  PINENE    U(I,J,S)                 BC(S)
 X  PINENE    DU(I,J,S) 0.0
 OD J
 IE S                   2
 Z  PINENE    V(I,S)                   BC(S)
 DO J         1                        NC
 X  PINENE    W(I,J,S)  0.0
 X  PINENE    DU(I,J,S) 0.0
 OD J
 IE S                   3
 Z  PINENE    V(I,S)                   BC(S)
 DO J         1                        NC
 X  PINENE    W(I,J,S)  0.0
 Z  PINENE    U(I,J,S)                 BC(S)
 X  PINENE    DU(I,J,S) 0.0
 OD J
 IE S                   4
 Z  PINENE    V(I,S)                   BC(S)
 DO J         1                        NC
 X  PINENE    W(I,J,S)  0.0
 X  PINENE    DU(I,J,S) 0.0
 OD J
 IE S                   5
 Z  PINENE    V(I,S)                   BC(S)
 DO J         1                        NC
 X  PINENE    W(I,J,S)  0.0
 Z  PINENE    U(I,J,S)                 BC(S)
 X  PINENE    DU(I,J,S) 0.0
 OD J
 ND

 DO K         2                        NM
 IA I1        I2        1
 A= RITAU     ITAU(K)
 IR I2        RITAU
 DO I         I1                       I2
 IE S                   1
 Z  PINENE    V(I,S)                   Z(K,S)
 DO J         1                        NC
 X  PINENE    W(I,J,S)  0.0
 Z  PINENE    U(I,J,S)                 Z(K,S)
 X  PINENE    DU(I,J,S) 0.0
 OD J
 IE S                   2
 Z  PINENE    V(I,S)                   Z(K,S)
 DO J         1                        NC
 X  PINENE    W(I,J,S)  0.0
 X  PINENE    DU(I,J,S) 0.0
 OD J
 IE S                   3
 Z  PINENE    V(I,S)                   Z(K,S)
 DO J         1                        NC
 X  PINENE    W(I,J,S)  0.0
 Z  PINENE    U(I,J,S)                 Z(K,S)
 X  PINENE    DU(I,J,S) 0.0
 OD J
 IE S                   4
 Z  PINENE    V(I,S)                   Z(K,S)
 DO J         1                        NC
 X  PINENE    W(I,J,S)  0.0
 X  PINENE    DU(I,J,S) 0.0
 OD J
 IE S                   5
 Z  PINENE    V(I,S)                   Z(K,S)
 DO J         1                        NC
 X  PINENE    W(I,J,S)  0.0
 Z  PINENE    U(I,J,S)                 Z(K,S)
 X  PINENE    DU(I,J,S) 0.0
 OD J
 ND

 IA I1        I2        1
 I= I2        NH
 DO I         I1                       I2
 IE S                   1
 Z  PINENE    V(I,S)                   Z(NM,S)
 DO J         1                        NC
 X  PINENE    W(I,J,S)  0.0
 Z  PINENE    U(I,J,S)                 Z(NM,S)
 X  PINENE    DU(I,J,S) 0.0
 OD J
 IE S                   2
 Z  PINENE    V(I,S)                   Z(NM,S)
 DO J         1                        NC
 X  PINENE    W(I,J,S)  0.0
 X  PINENE    DU(I,J,S) 0.0
 OD J
 IE S                   3
 Z  PINENE    V(I,S)                   Z(NM,S)
 DO J         1                        NC
 X  PINENE    W(I,J,S)  0.0
 Z  PINENE    U(I,J,S)                 Z(NM,S)
 X  PINENE    DU(I,J,S) 0.0
 OD J
 IE S                   4
 Z  PINENE    V(I,S)                   Z(NM,S)
 DO J         1                        NC
 X  PINENE    W(I,J,S)  0.0
 X  PINENE    DU(I,J,S) 0.0
 OD J
 IE S                   5
 Z  PINENE    V(I,S)                   Z(NM,S)
 DO J         1                        NC
 X  PINENE    W(I,J,S)  0.0
 Z  PINENE    U(I,J,S)                 Z(NM,S)
 X  PINENE    DU(I,J,S) 0.0
 OD J
 ND

ELEMENT TYPE

 EV PROD      THETA                    U
 EV PROD2     THETAA                   THETAB
 EV PROD2     U
 IV PROD2     THETA                    U

ELEMENT USES

 DO I         1                        NH
 DO J         1                        NC

 XT C1(I,J)   PROD2
 ZV C1(I,J)   THETAA                   THETA(1)
 ZV C1(I,J)   THETAB                   THETA(2)
 ZV C1(I,J)   U                        U(I,J,1)

 XT C2(I,J)   PROD
 ZV C2(I,J)   THETA                    THETA(1)
 ZV C2(I,J)   U                        U(I,J,1)

 XT C3(I,J)   PROD
 ZV C3(I,J)   THETA                    THETA(2)
 ZV C3(I,J)   U                        U(I,J,1)

 XT C4(I,J)   PROD2
 ZV C4(I,J)   THETAA                   THETA(3)
 ZV C4(I,J)   THETAB                   THETA(4)
 ZV C4(I,J)   U                        U(I,J,3)

 XT C5(I,J)   PROD
 ZV C5(I,J)   THETA                    THETA(5)
 ZV C5(I,J)   U                        U(I,J,5)

 XT C6(I,J)   PROD
 ZV C6(I,J)   THETA                    THETA(3)
 ZV C6(I,J)   U                        U(I,J,3)

 XT C7(I,J)   PROD
 ZV C7(I,J)   THETA                    THETA(4)
 ZV C7(I,J)   U                        U(I,J,3)

 ND

GROUP TYPE

 GV L2        X

GROUP USES

* collocation for equation 1 {i in 1..nh,j in 1..nc}:
* du[i,j,1] + (theta[1]+theta[2])*u[i,j,1] = 0

* collocation for equation 2 {i in 1..nh,j in 1..nc}:
* du[i,j,2] - theta[1]*u[i,j,1] = 0

* collocation for equation 3 {i in 1..nh,j in 1..nc}:
* du[i,j,3] - theta[2]*u[i,j,1] + (theta[3]+theta[4])*u[i,j,3] 
*           - theta[5]*u[i,j,5] = 0

* collocation for equation 4 {i in 1..nh,j in 1..nc}:
* du[i,j,4] - theta[3]*u[i,j,3] = 0

* collocation for equation 5 {i in 1..nh,j in 1..nc}:
* du[i,j,5] - theta[4]*u[i,j,3] + theta[5]*u[i,j,5] = 0

 DO I         1                        NH
 DO J         1                        NC
 XE CO(I,J,1) C1(I,J)   1.0
 XE CO(I,J,2) C2(I,J)   -1.0
 XE CO(I,J,3) C3(I,J)   -1.0           C4(I,J)   1.0           
 XE CO(I,J,3) C5(I,J)   -1.0
 XE CO(I,J,4) C6(I,J)   -1.0
 XE CO(I,J,5) C7(I,J)   -1.0           C5(I,J)   1.0           
 ND

*  objective function sum {j in 1..nm} obj(j)^2

 DO J         1                        NM
 DO S         1                        NE
 XT OBJ(J,S)  L2
 ND

OBJECT BOUND

*   Solution

*LO SOLUTION             1.98715D+01   $ (NH=25)
*LO SOLUTION             1.98721D+01   $ (NH=50)
*LO SOLUTION             1.98721D+01   $ (NH=100
*LO SOLUTION             1.98721D+01   $ (NH=200)

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      PINENE  

INDIVIDUALS

 T  PROD
 F                      THETA * U 
 G  THETA               U 
 G  U                   THETA
 H  THETA     U         1.0

 T  PROD2
 R  THETA     THETAA    1.0            THETAB    1.0
 R  U         U         1.0
 F                      THETA * U 
 G  THETA               U 
 G  U                   THETA
 H  THETA     U         1.0

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        PINENE  

INDIVIDUALS

 T  L2
 F                      X * X
 G                      X + X 
 H                      2.0

ENDATA
