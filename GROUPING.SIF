***************************
* SET UP THE INITIAL DATA *
***************************

NAME          GROUPING

*   Problem :
*   *********
*
*   A nonlinear formulation of a 0-1 combinatorial problem
*   which can be described as grouping and clustering.
*   This version of the problem consists in grouping 20 cans
*   into 5 groups and the objective is to obtain groups that
*   are as homogeneous as possible with respect to the characteristics
*   of the cans.
*   Note that the initial point is feasible.

*   This problem is the contribution of a LANCELOT user in order to fulfill
*   the package licence agreement.

*   Source :
*   D. Tuyttens, M. Pirlot, J. Teghem, E. Trauwaert and B. Liegeois,
*   "Homogeneous grouping of nuclear fuel cans through simulated
*   annealing and tabu search",
*   Annals of Operations Research 50 (1994) 575-607.

*   SIF input: D. Tuyttens (FPMs, Mons, Belgium) Dec 1994.

*   classification SQR2-MN-100-125

*   Problem parameters : Number of cans, groups and characteristics

 IE 1                   1
 IE LOTS                5
 IE POTS                20
 IE CAR                 2

*   The cans and their characteristics
*   First characteristic

 RE T1,1                4.37
 RE T2,1                4.56
 RE T3,1                4.26
 RE T4,1                4.56
 RE T5,1                4.30
 RE T6,1                4.46
 RE T7,1                3.84
 RE T8,1                4.57
 RE T9,1                4.26
 RE T10,1               4.37
 RE T11,1               3.49
 RE T12,1               4.43
 RE T13,1               4.48
 RE T14,1               4.01
 RE T15,1               4.29
 RE T16,1               4.42
 RE T17,1               4.23
 RE T18,1               4.42
 RE T19,1               4.23
 RE T20,1               3.49

*   Second characteristic

 RE T1,2                5.23
 RE T2,2                5.74
 RE T3,2                4.93
 RE T4,2                5.74
 RE T5,2                5.19
 RE T6,2                5.46
 RE T7,2                4.65
 RE T8,2                5.27
 RE T9,2                5.57
 RE T10,2               5.12
 RE T11,2               5.73
 RE T12,2               5.45
 RE T13,2               5.42
 RE T14,2               4.05
 RE T15,2               4.26
 RE T16,2               4.58
 RE T17,2               3.94
 RE T18,2               4.18
 RE T19,2               4.18
 RE T20,2               5.89

*   Average characteristics

 RE XM1                 17.1
 RE XM2                 20.1

*   Variables of the problem
*   X(j,k) = 1  can j belongs to group k
*          = 0  otherwise

VARIABLES

 DO J         1                        POTS
 DO K         1                        LOTS
 X  X(J,K)
 ND

*   Objective function and Constraints

GROUPS

 DO K         1                        LOTS
 DO I         1                        CAR
 DO J         1                        POTS
 ZN OBJ(K,I)  X(J,K)                   T(J,I)         
 ND

*   Linear constraints

 DO K         1                        LOTS
 DO J         1                        POTS
 XE CL1(K)    X(J,K)    1.0
 ND

 DO J         1                        POTS
 DO K         1                        LOTS
 XE CL2(J)    X(J,K)    1.0
 ND

*   Non-Linear constraints

 DO J         1                        POTS
 DO K         1                        LOTS
 XE CNL(J,K)  
 ND

* The constants of the problem.

CONSTANTS

 RE POTLOT              4.0

 DO K         1                        LOTS
 DO I         1                        CAR
 Z  GROUPING  OBJ(K,I)                 XM(I)
 ND

 DO K         1                        LOTS
 Z  GROUPING  CL1(K)                   POTLOT
 ND

 DO J         1                        POTS
 X  GROUPING  CL2(J)    1.0
 ND

* The bounds on the variables

BOUNDS      

 DO J         1                        POTS
 DO K         1                        LOTS
 XL GROUPING  X(J,K)    0.0
 XU GROUPING  X(J,K)    1.0
 ND

* The starting point

START POINT

 DO J         1                        POTS
 DO K         1                        LOTS
 XV GROUPING  X(J,K)    0.0
 ND

*  A feasible solution
*  This solution consists into setting the first 4 cans in 
*  group 1 and  the next 4 ones in the next group and so on.

 XV GROUPING  X1,1      1.0
 XV GROUPING  X2,1      1.0
 XV GROUPING  X3,1      1.0
 XV GROUPING  X4,1      1.0
 XV GROUPING  X5,2      1.0
 XV GROUPING  X6,2      1.0
 XV GROUPING  X7,2      1.0
 XV GROUPING  X8,2      1.0
 XV GROUPING  X9,3      1.0
 XV GROUPING  X10,3     1.0
 XV GROUPING  X11,3     1.0
 XV GROUPING  X12,3     1.0
 XV GROUPING  X13,4     1.0
 XV GROUPING  X14,4     1.0
 XV GROUPING  X15,4     1.0
 XV GROUPING  X16,4     1.0
 XV GROUPING  X17,5     1.0
 XV GROUPING  X18,5     1.0
 XV GROUPING  X19,5     1.0
 XV GROUPING  X20,5     1.0

* The nonlinear elements functions

ELEMENT TYPE

 EV BIN       XX

ELEMENT USES

 DO J         1                        POTS
 DO K         1                        LOTS
 XT BEL(J,K)  BIN
 ZV BEL(J,K)  XX                       X(J,K)
 ND

* The group functions

GROUP TYPE

 GV SQUARE    ALPHA

GROUP USES

 DO K         1                        LOTS
 DO I         1                        CAR
 XT OBJ(K,I)  SQUARE
 ND

 DO J         1                        POTS
 DO K         1                        LOTS
 XE CNL(J,K)  BEL(J,K)  1.0
 ND

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      GROUPING

INDIVIDUALS

 T  BIN
 F                      XX * ( XX - 1.0 )
 G  XX                  2.0 * XX - 1.0            
 H  XX        XX        2.0

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        GROUPING

INDIVIDUALS

 T  SQUARE
 F                      ALPHA * ALPHA
 G                      2.0 * ALPHA
 H                      2.0

ENDATA



