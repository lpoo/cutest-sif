***************************
* SET UP THE INITIAL DATA *
***************************

NAME          BDRY2

*   Problem :
*   *********

*   The bdry2_0 & _1.mod AMPL models from Hans Mittelmann (mittelmann@asu.edu)
*   See: http://plato.asu.edu/ftp/barrier/

*   SIF input: Nick Gould, April 25th 2012

*   classification QLR2-AN-V-V

*   the x-y discretization 

 IE N                   3              $-PARAMETER
 IE N                   499            $-PARAMETER
*IE N                   1999           $-PARAMETER     bdry2_0.mod value
*IE N                   3499           $-PARAMETER     bdry2_1.mod value

*   Other useful parameters

 IE 0                   0
 IE 1                   1
 IE 3                   3
 IE 4                   4
 RE ONE                 1.0

 IA N1        N         1
 I/ N1/4      N1                       4
 I* 3N1/4     N1/4                     3
 IA N1/4-1    N1/4      -1
 IA 3N1/4+1   3N1/4     1

 RE A                   0.0
 RE G                   0.0
 RE T                   5.0

 RI RN1       N1
 R/ H         ONE                      RN1 
 RM -H        H         -1.0
 R* H2        H                        H
 RM .5H2      H2        0.5
 R* HA        H                        A
 RM .5HA      HA        0.5
 R- 1-H       ONE                      H

 R* GH2       G                        H2
 R* HT        H                        T
 RM -HT       HT        -1.0

VARIABLES

 DO I         0                        N1
 DO J         0                        N1
 X  X(I,J)
 ND

GROUPS

 N  OBJ

 DO I         1                        N
 IA I+        I         1
 IA I-        I         -1
 DO J         1                        N
 IA J+        J         1
 IA J-        J         -1
 XE P(I,J)    X(I,J)    4.0
 XE P(I,J)    X(I,J+)   -1.0           X(I,J-)   -1.0
 XE P(I,J)    X(I+,J)   -1.0           X(I-,J)   -1.0
 ND

 DO I         1                        N
 XE B1(I)     X(I,0)    1.0            X(I,1)    -1.0
 ZE B2(I)     X(0,I)                   1-H
 XE B2(I)     X(1,I)    -1.0
 ZE B3(I)     X(N1,I)                  1-H
 XE B3(I)     X(N,I)    -1.0
 ND

RHS

 DO I         1                        N
 Z  BDRY2     B2(I)                    -HT
 Z  BDRY2     B3(I)                    -HT
 DO J         1                        N
 Z  BDRY2     P(I,J)                   GH2
 ND

BOUNDS
 
 LO BDRY2     'DEFAULT' 0.0
 UP BDRY2     'DEFAULT' 10.0

 DO I         N1/4                     3N1/4
 DO J         N1/4                     3N1/4
 XU BDRY2     X(I,J)    3.15
 ND

 XX BDRY2     X(0,0)    0.0
 XX BDRY2     X(N1,0)   0.0
 XX BDRY2     X(0,N1)   10.0
 XX BDRY2     X(N1,N1)  10.0

ELEMENT TYPE

 EV SQ        U
 EV SQD       Y
 EP SQD       YP

ELEMENT USES

 DO I         N1/4                     3N1/4
 DO J         N1/4                     3N1/4
 XT E(I,J)    SQD
 ZV E(I,J)    Y                        X(I,J)
 XP E(I,J)    YP        1.0
 ND

 DO I         1                        N
 XT E(I,N1)   SQ
 ZV E(I,N1)   U                        X(I,N1)
 ND

GROUP USES

 DO I         N1/4                     3N1/4
 DO J         N1/4                     3N1/4
 ZE OBJ       E(I,J)                   .5H2
 ND

 DO I         1                        N
 ZE OBJ       E(I,N1)                  .5HA
 ND

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      BDRY2

INDIVIDUALS

 T  SQ
 F                      U * U
 G  U                   2.0 * U
 H  U         U         2.0

 T  SQD
 F                      ( Y - YP ) ** 2
 G  Y                   2.0 * ( Y - YP )
 H  Y         Y         2.0

ENDATA
